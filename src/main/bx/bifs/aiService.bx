/**
 * [BoxLang]
 *
 * Copyright [2023] [Ortus Solutions, Corp]
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS"
 * BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 * ----------------------------------------------------------------------------------
 * Creates a reference to a registered AI Service Provider.
 */
@BoxBIF
class {

	/**
	 * Inject the following references into the class
	 * - moduleRecord : The ModuleRecord instance
	 * - boxRuntime : The BoxRuntime instance
	 * - interceptorService : The BoxLang InterceptorService
	 * - log : A logger for the module config itself
	 */
	static final CORE_PROVIDERS = [
		"claude",
		"deepseek",
		"gemini",
		"grok",
		"mistral",
		"huggingface",
		"groq",
		"ollama",
		"openai",
		"openrouter",
		"perplexity"
	];

    /**
     * Creates a reference to a registered AI Service Provider this module supports
	 * <p>
	 * You can pass an optional API key to use with the AI Service Provider to override the default API key from the configuration.
	 *
	 * @provider The provider to use. If not provided, the provider will use the default provider from the configuration.
	 * @apiKey The API key to use with the provider, if not provided the provider will use the default key from the configuration.
	 *
	 * @throws ProviderNotSupported if the provider is not supported
	 *
	 * @return The AI Service Provider
     */
    function invoke( string provider, any apiKey ) {
        // Default the provider from the module settings if not provided
		arguments.provider = arguments.provider ?: moduleRecord.settings.provider;

        // By convention check if there is a <PROVIDER>_API_KEY setting override it
        var conventionAPIKey = getSystemSetting( "#arguments.provider.ucase()#_API_KEY", "" );
        if( isNull( arguments.apiKey ) && len( conventionAPIKey ) ){
            arguments.apiKey = conventionAPIKey;
        }
        // Else use the module setting
		arguments.apiKey = arguments.apiKey ?: moduleRecord.settings.apiKey;

        // Build the target service
		var oTargetService = null;
		switch( arguments.provider ) {
			case "claude":
				oTargetService = new bxModules.bxai.models.providers.ClaudeService().configure( arguments.apiKey )
				break
			case "deepseek":
				oTargetService = new bxModules.bxai.models.providers.DeepSeekService().configure( arguments.apiKey )
				break
			case "gemini":
				oTargetService = new bxModules.bxai.models.providers.GeminiService().configure( arguments.apiKey )
				break
			case "grok":
				oTargetService = new bxModules.bxai.models.providers.GrokService().configure( arguments.apiKey )
				break
			case "mistral":
				oTargetService = new bxModules.bxai.models.providers.MistralService().configure( arguments.apiKey )
				break
			case "huggingface":
				oTargetService = new bxModules.bxai.models.providers.HuggingFaceService().configure( arguments.apiKey )
				break
			case "groq":
				oTargetService = new bxModules.bxai.models.providers.GroqService().configure( arguments.apiKey )
				break
			case "ollama":
				oTargetService = new bxModules.bxai.models.providers.OllamaService().configure( arguments.apiKey )
				break
			case "openai" :
				oTargetService = new bxModules.bxai.models.providers.OpenAIService().configure( arguments.apiKey )
				break
			case "openrouter":
				oTargetService = new bxModules.bxai.models.providers.OpenRouterService().configure( arguments.apiKey )
				break
			case "perplexity":
				oTargetService = new bxModules.bxai.models.providers.PerplexityService().configure( arguments.apiKey )
				break
			default:
				// Announce the provider request so contributors can listen and provide a service
				// This is for our aiplus module
				var iData = { provider: arguments.provider, apiKey: arguments.apiKey, service : null };
				BoxAnnounce( "onAIProviderRequest", iData );

				if( iData.service == null ){
					throw(
						type: "ProviderNotSupported",
						message: "Provider [#arguments.provider#] is not supported.  Valid Providers are: [#static.CORE_PROVIDERS.toList()#]"
					);
				}
				oTargetService = iData.service;
		}

		BoxAnnounce( "onAIProviderCreate", { provider: oTargetService } );

		return oTargetService;
	}

}
