/**
 * [BoxLang]
 *
 * Copyright [2023] [Ortus Solutions, Corp]
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS"
 * BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 * ----------------------------------------------------------------------------------
 * Base abstract class for all AI runnables.
 * Provides common functionality for naming, parameter management, and chaining operations.
 */
abstract class implements="IAiRunnable" {

	/**
	 * The name of the runnable
	 */
	property name="name" type="string" default="";

	/**
	 * Default parameters for this runnable
	 */
	property name="defaultParams" type="struct" default={};

	/**
	 * Run the operation with the given input
	 * Must be implemented by subclasses
	 *
	 * @input The input to process
	 * @params Optional parameters to configure the operation
	 *
	 * @return The result of the operation
	 */
	abstract public any function run(
		required any input,
		struct params = {}
	);

	/**
	 * Stream the operation with the given input, calling onChunk for each chunk
	 * Must be implemented by subclasses
	 *
	 * @input The input to process
	 * @onChunk The callback function to invoke for each chunk
	 * @params Optional parameters to configure the operation
	 */
	abstract public void function stream(
		required any input,
		required function onChunk,
		struct params = {}
	);

	/**
	 * Chain this runnable with another runnable
	 * Creates a sequence that will execute this runnable followed by the next
	 *
	 * @next The next runnable to execute
	 *
	 * @return A new AiRunnableSequence containing both runnables
	 */
	public IAiRunnable function to( required IAiRunnable next ) {
		return new AiRunnableSequence( [ this, arguments.next ] );
	}

	/**
	 * Apply a transformation function after this runnable
	 * Creates a sequence with a transform runnable
	 *
	 * @fn The transformation function to apply
	 *
	 * @return A new runnable sequence with the transform step
	 */
	public IAiRunnable function transform( required function fn ) {
		return this.to( new src.main.bx.models.transformers.AiTransformRunnable( fn = arguments.fn ) );
	}

	/**
	 * Set the name of this runnable
	 *
	 * @name The name to set
	 *
	 * @return This runnable for chaining
	 */
	public AiBaseRunnable function withName( required string name ) {
		variables.name = arguments.name;
		return this;
	}

	/**
	 * Get the name of this runnable
	 * Uses the explicitly set name if available, otherwise falls back to metadata
	 *
	 * @return The name of the runnable
	 */
	public string function getName() {
		if ( len( variables.name ) ) {
			return variables.name;
		}

		var md = getMetadata( this );
		if ( structKeyExists( md, "name" ) && len( md.name ) ) {
			return md.name;
		}
		if ( structKeyExists( md, "fullName" ) && len( md.fullName ) ) {
			return md.fullName;
		}

		return "IAiRunnable";
	}

	/**
	 * Set default parameters for this runnable
	 * Merges with existing default parameters
	 *
	 * @params The parameters to merge
	 *
	 * @return This runnable for chaining
	 */
	public AiBaseRunnable function withParams( required struct params ) {
		variables.defaultParams.append( arguments.params, true );
		return this;
	}

	/**
	 * Merge runtime parameters with default parameters
	 * Runtime parameters take precedence over defaults
	 *
	 * @params Runtime parameters to merge
	 *
	 * @return The merged parameter struct
	 */
	public struct function mergeParams( struct params = {} ) {
		var out = duplicate( variables.defaultParams );
		out.append( arguments.params, true );
		return out;
	}

}
