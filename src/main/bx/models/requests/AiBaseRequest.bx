/**
 * [BoxLang]
 *
 * Copyright [2023] [Ortus Solutions, Corp]
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS"
 * BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 * ----------------------------------------------------------------------------------
 * Base class for AI request models that provides common properties and methods
 */
abstract class{

	/**
	 * The params struct to pass to the provider in the request
	 */
	property name="params" type="struct" default={};

	/**
	 * The provider to use for the request
	 */
	property name="provider" default="";

	/**
	 * The API key to use for the provider
	 */
	property name="apiKey" default="";

	/**
	 * Log request
	 */
	property name="logRequest" type="boolean" default=false;

	/**
	 * Log request to console
	 * This is usually used for debugging purposes
	 */
	property name="logRequestToConsole" type="boolean" default=false;

	/**
	 * Log response
	 */
	property name="logResponse" type="boolean" default=false;

	/**
	 * Log response to console
	 * This is usually used for debugging purposes
	 */
	property name="logResponseToConsole" type="boolean" default=false;

	/**
	 * The return format of the response
	 */
	property name="returnFormat" default="raw";

	/**
	 * The model to use for the request
	 */
	property name="model" default="";

	/**
	 * The timeout for the request
	 */
	property name="timeout" type="numeric" default=30;

	/**
	 * Send an auth header or not with the API Key
	 * This is usually determined by the provider
	 */
	property name="sendAuthHeader" type="boolean" default=true;

	/**
	 * A collection of headers to send with the request
	 */
	property name="headers" type="struct" default={};

	/**
	 * Maximum number of tool call interactions allowed
	 * If not set, will use the service default (10)
	 */
	property name="maxInteractions" type="numeric" default=0;

	/**
	 * A context struct for injecting additional data into the request.
	 * This can be used for:
	 * - Security context (user roles, permissions, tenant info)
	 * - RAG context (retrieved documents, embeddings, metadata)
	 * - Any additional contextual information needed by agents or chat handlers
	 */
	property name="context" type="struct" default={};

	/**
	 * Add a header to the request
	 *
	 * @name The name of the header
	 * @value The value of the header
	 */
	function addHeader( required string name, required string value ){
		variables.headers[ arguments.name ] = arguments.value;
		return this;
	}

	/**
	 * Set the model if it's empty
	 *
	 * @model The model to use if the current model is empty
	 */
	function setModelIfEmpty( required string model ){
		if( variables.model.isEmpty() ){
			variables.model = arguments.model;
		}
		return this;
	}

	/**
	 * Set the API key if it's empty
	 *
	 * @apiKey The API key to use if the current API key is empty
	 */
	function setApiKeyIfEmpty( required string apiKey ){
		if( variables.apiKey.isEmpty() ){
			variables.apiKey = arguments.apiKey;
		}
		return this;
	}

	/**
	 * Merge service params into the request params
	 *
	 * @serviceParams The service params to merge
	 */
	function mergeServiceParams( required struct serviceParams ){
		// Filter out model as it's handled separately in some implementations
		var paramsToMerge = arguments.serviceParams.filter( ( key, value ) => key != "model" );
		variables.params.append( paramsToMerge, false );
		return this;
	}

	/**
	 * Merge service headers into the request headers
	 *
	 * @serviceHeaders The service headers to merge
	 */
	function mergeServiceHeaders( required struct serviceHeaders ){
		variables.headers.append( arguments.serviceHeaders, false );
		return this;
	}

	/**
	 * Check if the request has any context data
	 *
	 * @return True if context is not empty
	 */
	boolean function hasContext(){
		return !variables.context.isEmpty();
	}

	/**
	 * Add or update a single context value
	 *
	 * @key The context key
	 * @value The context value
	 *
	 * @return The request for chaining
	 */
	function addContext( required string key, required any value ){
		variables.context[ arguments.key ] = arguments.value;
		return this;
	}

	/**
	 * Merge additional context data into the existing context
	 * Existing keys will be overwritten if present in the new context
	 *
	 * @contextData The context data to merge
	 *
	 * @return The request for chaining
	 */
	function mergeContext( required struct contextData ){
		variables.context.append( arguments.contextData, true );
		return this;
	}

	/**
	 * Get a specific context value by key
	 *
	 * @key The context key to retrieve
	 * @defaultValue The default value if key doesn't exist
	 *
	 * @return The context value or default
	 */
	any function getContextValue( required string key, any defaultValue = "" ){
		return variables.context.keyExists( arguments.key ) ? variables.context[ arguments.key ] : arguments.defaultValue;
	}

}
