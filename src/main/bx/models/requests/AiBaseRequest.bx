/**
 * [BoxLang]
 *
 * Copyright [2023] [Ortus Solutions, Corp]
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS"
 * BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 * ----------------------------------------------------------------------------------
 * Base class for AI request models that provides common properties and methods
 */
abstract class{

	/**
	 * The params struct to pass to the provider in the request
	 */
	property name="params" type="struct" default={};

	/**
	 * The provider to use for the request
	 */
	property name="provider" default="";

	/**
	 * The API key to use for the provider
	 */
	property name="apiKey" default="";

	/**
	 * Log request
	 */
	property name="logRequest" type="boolean" default=false;

	/**
	 * Log request to console
	 * This is usually used for debugging purposes
	 */
	property name="logRequestToConsole" type="boolean" default=false;

	/**
	 * Log response
	 */
	property name="logResponse" type="boolean" default=false;

	/**
	 * Log response to console
	 * This is usually used for debugging purposes
	 */
	property name="logResponseToConsole" type="boolean" default=false;

	/**
	 * The return format of the response
	 */
	property name="returnFormat" default="raw";

	/**
	 * The model to use for the request
	 */
	property name="model" default="";

	/**
	 * The timeout for the request
	 */
	property name="timeout" type="numeric" default=30;

	/**
	 * Send an auth header or not with the API Key
	 * This is usually determined by the provider
	 */
	property name="sendAuthHeader" type="boolean" default=true;

	/**
	 * A collection of headers to send with the request
	 */
	property name="headers" type="struct" default={};

	/**
	 * Maximum number of tool call interactions allowed
	 * If not set, will use the service default (10)
	 */
	property name="maxInteractions" type="numeric" default=0;

	/**
	 * Tenant identifier for multi-tenant usage tracking
	 * Used to attribute AI usage to specific tenants for billing purposes
	 */
	property name="tenantId" type="string" default="";

	/**
	 * Additional metadata for usage tracking (cost center, project, etc.)
	 * This data will be included in onAITokenCount events for custom billing logic
	 */
	property name="usageMetadata" type="struct" default={};

	/**
	 * AWS Inference Profile ARN (optional, for Bedrock cost attribution)
	 * When set, Bedrock requests will use this inference profile for AWS-native cost tracking
	 */
	property name="inferenceProfileArn" type="string" default="";

	/**
	 * Add a header to the request
	 *
	 * @name The name of the header
	 * @value The value of the header
	 */
	function addHeader( required string name, required string value ){
		variables.headers[ arguments.name ] = arguments.value;
		return this;
	}

	/**
	 * Set the model if it's empty
	 *
	 * @model The model to use if the current model is empty
	 */
	function setModelIfEmpty( required string model ){
		if( isNull( arguments.model ) || arguments.model.isEmpty() ){
			return this;
		}

		if( variables.model.isEmpty() ){
			variables.model = arguments.model;
		}
		return this;
	}

	/**
	 * Get Model if set or looks up from params
	 *
	 * @return The model to use
	 */
	function getModel(){
		if( !variables.model.isEmpty() ){
			return variables.model;
		}
		if( variables.params.keyExists( "model" ) ){
			return variables.params.model;
		}
		return "";
	}

	/**
	 * Set the API key if it's empty
	 *
	 * @apiKey The API key to use if the current API key is empty
	 */
	function setApiKeyIfEmpty( required string apiKey ){
		if( variables.apiKey.isEmpty() ){
			variables.apiKey = arguments.apiKey;
		}
		return this;
	}

	/**
	 * Merge service params into the request params
	 *
	 * @serviceParams The service params to merge
	 * @override If true, override existing params with service params
	 *
	 * @return This request
	 */
	AiBaseRequest function mergeServiceParams( required struct serviceParams, boolean override = false ){
		// Filter out model as it's handled separately in some implementations
		//var paramsToMerge = arguments.serviceParams.filter( ( key, value ) => key != "model" );
		variables.params.append( arguments.serviceParams, arguments.override );
		return this;
	}

	/**
	 * Merge service headers into the request headers
	 *
	 * @serviceHeaders The service headers to merge
	 * @override If true, override existing headers with service headers
	 *
	 * @return This request
	 */
	AiBaseRequest function mergeServiceHeaders( required struct serviceHeaders, boolean override = false ){
		variables.headers.append( arguments.serviceHeaders, arguments.override );
		return this;
	}

	/**
	 * Set the tenant identifier for multi-tenant usage tracking
	 *
	 * @tenantId The tenant identifier
	 *
	 * @return The request for chaining
	 */
	function setTenantId( required string tenantId ){
		variables.tenantId = arguments.tenantId;
		return this;
	}

	/**
	 * Set the usage metadata for tracking purposes
	 *
	 * @usageMetadata A struct containing custom tracking data (cost center, project, etc.)
	 *
	 * @return The request for chaining
	 */
	function setUsageMetadata( required struct usageMetadata ){
		variables.usageMetadata = arguments.usageMetadata;
		return this;
	}

	/**
	 * Set the AWS Inference Profile ARN for Bedrock cost attribution
	 *
	 * @inferenceProfileArn The AWS Inference Profile ARN
	 *
	 * @return The request for chaining
	 */
	function setInferenceProfileArn( required string inferenceProfileArn ){
		variables.inferenceProfileArn = arguments.inferenceProfileArn;
		return this;
	}

}
