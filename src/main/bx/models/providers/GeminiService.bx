/**
 * [BoxLang]
 *
 * Copyright [2023] [Ortus Solutions, Corp]
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS"
 * BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 * ----------------------------------------------------------------------------------
 */
class extends="BaseService"{

	/**
	 * Constructor
	 */
	function init(){
		variables.chatURL = "https://generativelanguage.googleapis.com/v1beta/models/%MODEL%:generateContent"
		variables.name = "Gemini"
		defaults( {
			// according to the docs, this is the default model
			"model" : "gemini-2.0-flash"
		} )
	}

	/**
	 * Gemini chat request
	 */
	@override
	public function chat( required ChatRequest chatRequest ){
		// We need to pre-process the messages to conform to the Gemini API
		var dataPacket = {
			"contents": arguments.chatRequest
				.getMessages()
				.map( message -> {
					return {
						"role" : message.role == "system" ? "model" : "user",
						"parts": [ {
							"text": message.content
						} ]
					}
				} )
			}.append( arguments.chatRequest.getParams() )

		// Tooling support
		if( dataPacket.keyExists( "tools" ) ){
			dataPacket.tools = dataPacket.tools.map( .getSchema )
		}

		// Update URL according to the model
		variables.chatURL = getChatUrl().replace( '%MODEL%', chatRequest.getModel() ) & '?key=#chatRequest.getApiKey()#';
		// No auth header
		arguments.chatRequest.setSendAuthHeader( false );

		// Send it
		var result = sendRequest( chatRequest, dataPacket )

		// If an error is returned, throw it
		if( result.keyExists( "error" ) ){
			throw(
				type   : "ProviderError",
				message: result.error.toString()
			);
		}

		// No Tool calls yet.

		// Determine return formats
		switch( chatRequest.getReturnFormat() ){
			case "all":
				return result.candidates;
			case "raw":
				return result;
			case "single": default:
				return result.candidates.first().content.parts.first().text
		}

		/**
		 * ---------------------------------------------------------------------------------------------------------
		 * Tool Chains
		 * ---------------------------------------------------------------------------------------------------------
		 * TODO by Jacob here.
		 */

	}

}
