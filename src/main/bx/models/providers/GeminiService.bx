/**
 * [BoxLang]
 *
 * Copyright [2023] [Ortus Solutions, Corp]
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS"
 * BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language
 * governing permissions and limitations under the License.
 * ----------------------------------------------------------------------------------
 */
class extends="BaseService"{

	/**
	 * Constructor
	 */
	function init(){
		variables.chatURL = "https://generativelanguage.googleapis.com/v1beta/models/%MODEL%:generateContent"
		variables.name = "Gemini"
		defaults( {
			// https://ai.google.dev/gemini-api/docs/quickstart
			// according to the docs, this is the default model
			"model" : "gemini-2.5-flash"
		} )
	}

	/**
	 * Gemini chat request
	 */
	@override
	public function chat( required AiRequest aiRequest ){
		// We need to pre-process the messages to conform to the Gemini API
		var dataPacket = {
			"contents": arguments.aiRequest
				.getMessages()
				.map( message -> {
					return {
						"role" : message.role == "system" ? "model" : "user",
						"parts": [ {
							"text": message.content
						} ]
					}
				} )
			}.append( arguments.aiRequest.getParams() )

		// Tooling support
		if( dataPacket.keyExists( "tools" ) ){
			dataPacket.tools = dataPacket.tools.map( .getSchema )
		}

		// Update URL according to the model
		variables.chatURL = getChatUrl().replace( '%MODEL%', chatRequest.getModel() ) & '?key=#chatRequest.getApiKey()#';
		// No auth header
		arguments.aiRequest.setSendAuthHeader( false );

		// Send it
		var result = sendRequest( chatRequest, dataPacket )

		// If an error is returned, throw it
		if( result.keyExists( "error" ) ){
			throw(
				type   : "ProviderError",
				message: result.error.toString()
			);
		}

		// No Tool calls yet.

		// Determine return formats
		switch( chatRequest.getReturnFormat() ){
			case "all":
				return result.candidates;
			case "raw":
				return result;
			case "single": default:
				return result.candidates.first().content.parts.first().text
		}
	}

	/**
	 * Gemini streaming chat request - overridden to use Gemini-specific streaming format
	 * Gemini uses streamGenerateContent endpoint with ?alt=sse parameter
	 * @see https://ai.google.dev/gemini-api/docs/text-generation#rest_5
	 *
	 * @aiRequest The AiRequestobject to send to the provider
	 * @callback A callback function to be called with each chunk of the stream
	 */
	@override
	public function chatStream( required AiRequest aiRequest, required function callback ){
		// We need to pre-process the messages to conform to the Gemini API
		var dataPacket = {
			"contents": arguments.aiRequest
				.getMessages()
				.map( message -> {
					return {
						"role" : message.role == "system" ? "model" : "user",
						"parts": [ {
							"text": message.content
						} ]
					}
				} )
			}.append( arguments.aiRequest.getParams() )

		// Tooling support
		if( dataPacket.keyExists( "tools" ) ){
			dataPacket.tools = dataPacket.tools.map( .getSchema )
		}

		// Update URL for Gemini streaming: use streamGenerateContent endpoint with alt=sse
		var streamingURL = getChatUrl()
			.replace( '%MODEL%', chatRequest.getModel() )
			.replace( 'generateContent', 'streamGenerateContent' )
			& '?key=#chatRequest.getApiKey()#&alt=sse';

		// Set the streaming URL
		variables.chatURL = streamingURL;

		// No auth header for Gemini
		arguments.aiRequest.setSendAuthHeader( false );

		// Send it with streaming
		sendStreamRequest( chatRequest, dataPacket, callback )
	}

}
