/**
 * Base Service for AI Providers
 * This service provides a base implementation for AI providers
 * It is based on the Open AI standard, overridable by the provider
 * ----
 * All AI Providers must extend this service and implement the following methods:
 * - configure()
 * - invoke()
 *
 * All providers have the following properties:
 * - apiKey                                   : The API key to use with the provider
 * - chatURL                                  : The chat URL of the provider API
 * - params                                   : The default params request properties
 */
abstract class implements="IService" {

	/**
	 * The name of the LLM
	 */
	property name="name" default="";

	/**
	 * The API key to use with the provider
	 */
	property name = "apiKey" default = "";

	/**
	 * The chat URL of the provider API
	 */
	property name = "chatURL" default = "";

	/**
	 * The embedding URL of the provider API
	 */
	property name = "embedURL" default = "";

	/**
	 * The default params to use with the provider
	 */
	property name = "params" type = "struct" default = {};

	/**
	 * The default headers to use with the provider
	 */
	property name = "headers" type = "struct" default = {};

	/**
	 * Constants
	 */
	static {
		final settings = getModuleInfo( "bxai" ).settings
	}

	/**
	 * ---------------------------------------------------------------------------------------------------------
	 * Helper Methods
	 * ---------------------------------------------------------------------------------------------------------
	 */

	/**
	 * Set the default params for the provider
	 *
	 * @params - The params to set as defaults
	 *
	 * @return The service instance
	 */
	IService function defaults( required params ){
		variables.params.append( arguments.params, true );
		return this;
	}

	/**
	 * ---------------------------------------------------------------------------------------------------------
	 * Interface Methods
	 * ---------------------------------------------------------------------------------------------------------
	 */

	 /**
	 * Configure the service with the API key
	 *
	 * @apiKey - The API key to use with the provider
	 *
	 * @return The service instance
	 */
	IService function configure( required any apiKey ){
		variables.apiKey = arguments.apiKey;
		return this;
	}

	/**
	 * Add a header to the service definition
	 *
	 * @name The name of the header
	 * @value The value of the header
	 */
	BaseService function addHeader( required string name, required string value ){
		variables.headers[ arguments.name ] = arguments.value;
		return this;
	}

	/**
	 * Invoke a request to the provider
	 *
	 * @chatRequest The Chat request to send to the provider
	 *
	 * @return The response from the provider according to the return format in the chat request
	 */
	function invoke( required ChatRequest chatRequest ){
		// Model Selection if not set, use the default in the service, which should always be set
		chatRequest
			.setModelIfEmpty( variables.params.model )
			.setApiKeyIfEmpty( getAPIKey() )
			.mergeServiceParams( variables.params )
			.mergeServiceHeaders( variables.headers )
		// Do a chat request
		// MORE TYPES CAN BE ADDED HERE LATER
		return chat( argumentCollection = arguments )
	}

	/**
	 * A chat method that sends messages to the provider.
	 * This method in this base class is based of OpenAI's standard.
	 * If the provider does not support this standard, it should override this method.
	 *
	 * @chatRequest The ChatRequest object to send to the provider
	 *
	 * @throws ProviderError if the provider returns an error from the request
	 *
	 * @return The response from the provider according to the return format in the chat request
	 */
	public function chat( required ChatRequest chatRequest ){
		// Build the packet according to the OpenAI standard
		var dataPacket = {
			"model"   : arguments.chatRequest.getModel(),
			"messages": arguments.chatRequest.getMessages()
		}.append( arguments.chatRequest.getParams() )

		// Tooling support
		if( dataPacket.keyExists( "tools" ) ){
			dataPacket.tools = dataPacket.tools.map( .getSchema )
		}

		// Send it
		var result = sendRequest( chatRequest, dataPacket )

		// If an error is returned, throw it
		if( result.keyExists( "error" ) ){
			writeLog(
				text: results.error.toString(),
				type: "error",
				log : "ai"
			)
			throw(
				type   : "ProviderError",
				message: result.error.toString()
			);
		}

		// Result returns, only if we are not using tool calls
		if( !result.choices.first().message.keyExists( "tool_calls" ) ){
			// Determine return formats
			switch( chatRequest.getReturnFormat() ){
				case "all":
					return result.choices;
				case "raw":
					return result;
				case "single": default:
					return result.choices.first().message.content;
			}
		}

		/**
		 * ---------------------------------------------------------------------------------------------------------
		 * Tool Chains
		 * ---------------------------------------------------------------------------------------------------------
		 */
		var newMessages = chatRequest.getMessages().map( message -> message );
		result.choices.each( ( choice, i ) => {

			// add the tool call into our message history
			newMessages.append( choice.message );

			// find the tool, invoke it, append the result to the chat history
			choice.message.tool_calls.each( ( toolCall, i ) => {
				chatRequest.getTool( toolCall.function.name )
					.ifPresentOrElse(
						tool => {
							newMessages.append({
								"role"        : "tool",
								"tool_call_id": toolCall.id,
								"content"     : tool.invoke( JSONDeserialize( toolCall.function.arguments ) )
							});
						},
						() => {
							writeLog(
								text: "Unable to find tool named: #toolCall.function.name#",
								type: "warning",
								log : "ai"
							)
							newMessages.append({
								"role"        : "tool",
								"tool_call_id": toolCall.id,
								"content"     : "Tool ['#toolCall.function.name#'] not found in chat request"
							});
						}
					)
			});
		});

		chatRequest.setMessages( newMessages )

		return chat( chatRequest )
	}

	/**
	 * A generic HTTP proxy to send requests to the provider
	 *
	 * @chatRequest The ChatRequest object to use in the request
	 * @dataPacket The data packet to send to the provider
	 *
	 * @return The response from the provider as a struct
	 */
	private function sendRequest( required ChatRequest chatRequest, required struct dataPacket ){
		// Announce the request
		BoxAnnounce(
			"onAIRequest",
			{
				"dataPacket" : arguments.dataPacket,
				"chatRequest": arguments.chatRequest,
				"provider"   : this
			}
		);

		// Log the request
		if( arguments.chatRequest.getLogRequest() ){
			writeLog(
				text: "Request to AI Provider: #arguments.dataPacket.toString()#",
				type: "info",
				log : "ai"
			)
		}

		// Log the request to the console if enabled
		if( arguments.chatRequest.getLogRequestToConsole() ){
			println( "AI Request" )
			println( arguments.dataPacket )
		}

		bx: http
			url     = getChatURL()
			method  = "post"
			result  = "chatResult"
			charset = "utf-8"
			timeout = arguments.chatRequest.getTimeout()
		{
			bx:httpParam type="header" name="content-type" value="application/json";

			// Auth Header ONLY if the provider requires it
			// This is the default for OpenAI
			// If the provider does not require it, set it to false
			if( arguments.chatRequest.getSendAuthHeader() ){
				bx:httpParam type="header" name="Authorization" value="Bearer #arguments.chatRequest.getApiKey()#";
			}

			// Custom Headers
			for( var thisHeader in arguments.chatRequest.getHeaders() ){
				bx:httpParam
					type="header"
					name="#thisHeader#"
					value="#arguments.chatRequest.getHeaders()[ thisHeader ]#";
			}

			// Body Packet
			bx:httpParam type="body" value=jsonSerialize( arguments.dataPacket );
		}

		// Final logging if the provider supports it
		if( arguments.chatRequest.getLogResponse() ){
			writeLog(
				text: "Reponse from AI Provider: #chatResult.toString()#",
				type: "info",
				log : "ai"
			)
		}

		// Log the response to the console if enabled
		if( arguments.chatRequest.getLogResponseToConsole() ){
			println( "AI Response" )
			println( chatResult )
			println( "AI Deserialized Response" )
			println( jsonDeserialize( chatResult.filecontent ) )
		}

		var iData = {
			"chatRequest" = arguments.chatRequest,
			"response" = jsonDeserialize( chatResult.filecontent ),
			"rawResponse"	: chatResult,
			"provider" = this
		}

		BoxAnnounce( "onAIResponse", iData );

		return iData.response;
	}

	/**
	 * Send an embedding request to the provider
	 */
	public function sendEmbedRequest( required any embedRequest, required any dataPacket ){
		// Announce the request
		BoxAnnounce(
			"onAIEmbedRequest",
			{
				"dataPacket" : arguments.dataPacket,
				"embedRequest": arguments.embedRequest,
				"provider"   : this
			}
		);

		// Log the request
		if( structKeyExists(arguments.embedRequest, 'getLogRequest') && arguments.embedRequest.getLogRequest() ){
			writeLog(
				text: "Request to AI Provider (Embed): #arguments.dataPacket.toString()#",
				type: "info",
				log : "ai"
			)
		}

		// Log the request to the console if enabled
		if( structKeyExists(arguments.embedRequest, 'getLogRequestToConsole') && arguments.embedRequest.getLogRequestToConsole() ){
			println( "AI Embed Request" )
			println( arguments.dataPacket )
		}

		var embedURL = structKeyExists(this, 'getEmbedURL') ? this.getEmbedURL() : this.getChatURL();
		var timeout = arguments.embedRequest.timeout.len() ? arguments.embedRequest.timeout : 30;

		bx: http
			url     = embedURL
			method  = "post"
			result  = "embedResult"
			charset = "utf-8"
			timeout = timeout
		{
			bx:httpParam type="header" name="content-type" value="application/json";

			// Auth Header ONLY if the provider requires it
			if( structKeyExists(arguments.embedRequest, 'getSendAuthHeader') && arguments.embedRequest.getSendAuthHeader() ){
				bx:httpParam type="header" name="Authorization" value="Bearer #structKeyExists(arguments.embedRequest, 'getApiKey') ? arguments.embedRequest.getApiKey() : ''#";
			}

			// Custom Headers
			if( structKeyExists(arguments.embedRequest, 'getHeaders') && isStruct(arguments.embedRequest.getHeaders()) ){
				for( var thisHeader in arguments.embedRequest.getHeaders() ){
					bx:httpParam
						type="header"
						name="#thisHeader#"
						value="#arguments.embedRequest.getHeaders()[ thisHeader ]#";
				}
			}

			// Body Packet
			bx:httpParam type="body" value=jsonSerialize( arguments.dataPacket );
		}

		// Final logging if the provider supports it
		if( structKeyExists(arguments.embedRequest, 'getLogResponse') && arguments.embedRequest.getLogResponse() ){
			writeLog(
				text: "Response from AI Provider (Embed): #embedResult.toString()#",
				type: "info",
				log : "ai"
			)
		}

		// Log the response to the console if enabled
		if( structKeyExists(arguments.embedRequest, 'getLogResponseToConsole') && arguments.embedRequest.getLogResponseToConsole() ){
			println( "AI Embed Response" )
			println( embedResult )
			println( "AI Embed Deserialized Response" )
			println( jsonDeserialize( embedResult.filecontent ) )
		}

		var iData = {
			"embedRequest" = arguments.embedRequest,
			"response" = jsonDeserialize( embedResult.filecontent ),
			"rawResponse" : embedResult,
			"provider" = this
		}

		BoxAnnounce( "onAIEmbedResponse", iData );

		return iData.response;
	}

}
